/-
Copyright (c) 2025 CompPoly. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao, Gregor Mitscha-Baude, Derek Sorensen
-/
import Mathlib.Algebra.Tropical.Basic
import Mathlib.RingTheory.Polynomial.Basic
import CompPoly.Data.Array.Lemmas
import CompPoly.Univariate.Basic
import CompPoly.Univariate.Quotient

/-!
  # Canonical Univariate Polynomials

  This file defines `CPolynomialC R`, the type of canonical (trimmed) univariate polynomials.
  A polynomial is canonical if it has no trailing zeros, i.e., `p.trim = p`.

  This provides a unique representation for each polynomial, enabling stronger extensionality
  properties compared to the raw `CPolynomial` type.
-/
namespace CompPoly

namespace CPolynomial

variable {R : Type*} [Ring R] [BEq R]
variable {Q : Type*} [Ring Q]

/-- Canonical univariate polynomials: those with no trailing zeros.

  A polynomial `p : CPolynomial R` is canonical if `p.trim = p`, meaning the last coefficient
  is non-zero (or the polynomial is empty). This provides a unique representative for each
  polynomial equivalence class.

  TODO: make THIS the `CPolynomial`, rename current `CPolynomial` to `CPolynomial.Raw` or something,
  changing this file to Basic.lean? -/
def CPolynomialC (R : Type*) [BEq R] [Ring R] := { p : CPolynomial R // p.trim = p }

/-- Extensionality for canonical polynomials. -/
@[ext] theorem CPolynomialC.ext {p q : CPolynomialC R} (h : p.val = q.val) : p = q := by
  exact Subtype.ext h

/-- Canonical polynomials coerce to raw polynomials. -/
instance : Coe (CPolynomialC R) (CPolynomial R) where coe := Subtype.val

/-- The zero polynomial is canonical. -/
instance : Inhabited (CPolynomialC R) := ⟨#[], Trim.canonical_empty⟩

-- TODO namespace organization may have to change for ergonomics, etc
--      especially wrt the typeclass instances below
namespace OperationsC

variable {R : Type*} [Ring R] [BEq R] [LawfulBEq R]
variable (p q r : CPolynomialC R)

/-- Addition of canonical polynomials (result is canonical). -/
instance : Add (CPolynomialC R) where
  add p q := ⟨p.val + q.val, by apply Trim.trim_twice⟩

theorem add_comm : p + q = q + p := by
  apply CPolynomialC.ext; apply CPolynomial.add_comm

theorem add_assoc : p + q + r = p + (q + r) := by
  apply CPolynomialC.ext; apply CPolynomial.add_assoc

instance : Zero (CPolynomialC R) := ⟨0, zero_canonical⟩

theorem zero_add : 0 + p = p := by
  apply CPolynomialC.ext
  apply CPolynomial.zero_add p.val p.prop

theorem add_zero : p + 0 = p := by
  apply CPolynomialC.ext
  apply CPolynomial.add_zero p.val p.prop

/-- Scalar multiplication by a natural number (result is canonical). -/
def nsmul (n : ℕ) (p : CPolynomialC R) : CPolynomialC R :=
  ⟨CPolynomial.nsmul n p.val, by apply Trim.trim_twice⟩

theorem nsmul_zero : nsmul 0 p = 0 := by
  apply CPolynomialC.ext; apply CPolynomial.nsmul_zero

theorem nsmul_succ (n : ℕ) (p : CPolynomialC R) : nsmul (n + 1) p = nsmul n p + p := by
  apply CPolynomialC.ext; apply CPolynomial.nsmul_succ

instance : Neg (CPolynomialC R) where
  neg p := ⟨-p.val, neg_trim p.val p.prop⟩

instance : Sub (CPolynomialC R) where
  sub p q := p + -q

theorem neg_add_cancel : -p + p = 0 := by
  apply CPolynomialC.ext
  apply CPolynomial.neg_add_cancel

instance [LawfulBEq R] : AddCommGroup (CPolynomialC R) where
  add_assoc := add_assoc
  zero_add := zero_add
  add_zero := add_zero
  add_comm := add_comm
  neg_add_cancel := neg_add_cancel
  nsmul := nsmul -- TODO do we actually need this custom implementation?
  nsmul_zero := nsmul_zero
  nsmul_succ := nsmul_succ
  zsmul := zsmulRec -- TODO do we want a custom efficient implementation?

section MulOne

variable [Nontrivial R]

/-- Multiplication of canonical polynomials (result is canonical).

  The product of two canonical polynomials is canonical because multiplication
  preserves the "no trailing zeros" property.
-/
instance : Mul (CPolynomialC R) where
  mul p q :=
    ⟨p.val * q.val, by exact mul_is_trimmed p.val q.val⟩

/-- The constant polynomial 1 is canonical, and is the Unit for mutliplication.

  This is `#[1]`, which has no trailing zeros.

  This proof does not work without the assumption that R is non-trivial.
-/
instance : One (CPolynomialC R) where
  one := ⟨CPolynomial.C 1, by
  unfold C trim
  have nonzero : #[(1 : R)].size = 1 := by aesop
  have : lastNonzero #[(1 : R)] = some ⟨0, by simp⟩ := by
    unfold lastNonzero Array.findIdxRev? Array.findIdxRev?.find
    simp; aesop
  rw[this]
  grind
  ⟩

/-- Construct a canonical monomial `c * X^n` as a `CPolynomialC R`.

  The result is canonical (no trailing zeros) when `c ≠ 0`.
  For example, `monomialC 2 3` represents `3 * X^2`.

  Note: If `c = 0`, this returns `0` (the zero polynomial).
-/
def monomialC [DecidableEq R] (n : ℕ) (c : R) : CPolynomialC R :=
  ⟨CPolynomial.monomial n c, by
    unfold monomial
    split_ifs with hc
    · exact Trim.canonical_empty
    · apply Trim.canonical_iff.mpr
      intro hp
      simp only [mk]
      simp only [Array.append_singleton, ne_eq]
      refine (Trim.lastNonzero_last_iff (id (Eq.refl ((Array.replicate n 0).push c)) ▸ hp)).mp ?_
      refine (Trim.lastNonzero_last_iff hp).mpr ?_
      refine (Trim.lastNonzero_last_iff hp).mp ?_
      rw [Trim.lastNonzero_last_iff hp]
      simp only [mk]
      convert hc using 1
      simp [Array.getLast]
      grind
    ⟩

/-- Natural number degree of a canonical polynomial.

  Returns the degree as a natural number. For the zero polynomial, returns `0`.
  This matches Mathlib's `Polynomial.natDegree` API.
-/
def natDegree (p : CPolynomialC R) : ℕ := p.val.natDegree

-- TODO: Prove multiplicative properties, e.g.

lemma one_mul (p : CPolynomialC R) : 1 * p = p := by
  apply Subtype.ext
  have : (1 * p : CPolynomialC R) = (1: CPolynomial R) * p.val := rfl
  rw[this, one_mul_trim]
  exact p.prop

lemma mul_one (p : CPolynomialC R) : p * 1 = p := by
  apply Subtype.ext
  have : (p * 1 : CPolynomialC R) = p.val * (1: CPolynomial R) := rfl
  rw[this, mul_one_trim]
  exact p.prop

omit [Nontrivial R] in
lemma mul_assoc (p q r : CPolynomialC R) : (p * q) * r = p * (q * r) := by
  apply Subtype.ext
  exact CPolynomial.mul_assoc p.val q.val r.val

omit [Nontrivial R] in
lemma zero_mul (p : CPolynomialC R) : 0 * p = 0 := by
  apply Subtype.ext
  exact CPolynomial.zero_mul p.val

omit [Nontrivial R] in
lemma mul_zero (p : CPolynomialC R) : p * 0 = 0 := by
  apply Subtype.ext
  exact CPolynomial.mul_zero p.val

omit [Nontrivial R] in
lemma left_distrib (p q r : CPolynomialC R) : p * (q + r) = p * q + p * r := by
  apply Subtype.ext
  exact CPolynomial.left_distrib p.val q.val r.val

omit [Nontrivial R] in
lemma right_distrib (p q r : CPolynomialC R) : (p + q) * r = p * r + q * r := by
  apply Subtype.ext
  exact CPolynomial.right_distrib p.val q.val r.val

end MulOne

section Semiring

/-- `CPolynomialC R` forms a semiring when `R` is a semiring.

  The semiring structure extends the `AddCommGroup` structure with multiplication.
  All operations preserve the canonical property (no trailing zeros).

  TODO: Complete all the required proofs for the semiring axioms.
-/
instance [Semiring R] [LawfulBEq R] [Nontrivial R] : Semiring (CPolynomialC R) where
  add_assoc := add_assoc
  zero_add := zero_add
  add_zero := add_zero
  add_comm := add_comm
  zero_mul := zero_mul
  mul_zero := mul_zero
  mul_assoc := mul_assoc
  one_mul := one_mul
  mul_one := mul_one
  left_distrib := left_distrib
  right_distrib := right_distrib
  nsmul := nsmul
  nsmul_zero := nsmul_zero
  nsmul_succ := nsmul_succ
  npow n p := ⟨p.val ^ n, by sorry⟩
  npow_zero := by intro x; apply Subtype.ext; rfl
  npow_succ := by intro n p; sorry
  natCast_zero := by rfl
  natCast_succ := by intro n; rfl

end Semiring

section CommSemiring

/-- `CPolynomialC R` forms a commutative semiring when `R` is a commutative semiring.

  Commutativity follows from the commutativity of multiplication in the base ring.
-/
instance [CommSemiring R] [LawfulBEq R] [Nontrivial R] : CommSemiring (CPolynomialC R) where
  mul_comm := by sorry

end CommSemiring

section Ring

/-- `CPolynomialC R` forms a ring when `R` is a ring.

  The ring structure extends the semiring structure with negation and subtraction.
  Most of the structure is already provided by the `Semiring` instance.
-/
instance [Ring R] [LawfulBEq R] [Nontrivial R] : Ring (CPolynomialC R) where
  sub_eq_add_neg := by intro a b; rfl
  zsmul := zsmulRec
  zsmul_zero' := by sorry
  zsmul_succ' := by sorry
  zsmul_neg' := by sorry
  intCast_ofNat := by sorry
  intCast_negSucc := by sorry
  neg_add_cancel := by sorry

end Ring

section CommRing

/-- `CPolynomialC R` forms a commutative ring when `R` is a commutative ring.

  This combines the `CommSemiring` and `Ring` structures.
-/
instance [CommRing R] [LawfulBEq R] [Nontrivial R] : CommRing (CPolynomialC R) where
  -- All structure inherited from `CommSemiring` and `Ring` instances

end CommRing

end OperationsC

-- TODO: Ring equivalence between canonical polynomials and the quotient.
-- This establishes that `CPolynomialC R` and `QuotientCPolynomial R` are isomorphic
-- as rings. The canonical polynomials serve as unique representatives of each
-- equivalence class in the quotient.
--
-- TODO: Construct this ring equivalence after proving:
-- 1. Operations on quotient correspond to operations on canonical representatives
-- 2. `trim` gives a unique representative for each equivalence class
--
-- The equivalence should map canonical polynomials to their quotient classes
-- and back, preserving all ring operations.
-- TODO: Implement `ringEquivQuotient : CPolynomialC R ≃+* QuotientCPolynomial R`

end CPolynomial

end CompPoly
